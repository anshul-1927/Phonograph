buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

// For pushing APKs directly to Google Play. Won't work without the .p12 key.
play {
    serviceAccountEmail = '992499570703-v2bp8sl246qm0tblmfv4s45v6njhdfr2@developer.gserviceaccount.com'
    pk12File = file('../privatekeys/play-developer-key.p12')
    track = 'beta'
}

android {
    signingConfigs {
        debug {
            keyAlias 'phonograph-debug'
            keyPassword 'debugkey'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'
        }
        release {
            keyAlias 'phonograph'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'

            // "keystore.properties" file containing the release keys password is needed in order to sign the apk for release
            File propFile = project.rootProject.file('privatekeys/keystore.properties')
            if (propFile.exists()) {
                Properties properties = new Properties()
                properties.load(propFile.newDataInputStream())

                keyPassword properties.getProperty('keyPassword')
            } else {
                println 'keystore.properties not found'
                keyPassword null
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23

        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true

        applicationId "com.kabouzeid.gramophone"
        versionCode 80
        versionName "0.9.47 beta 1"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
        // remove as soon as it has been fixed
        disable 'PrivateResource'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:support-v13:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:gridlayout-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:palette-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:support-annotations:23.1.0'

    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'asia.ivity.android:drag-sort-listview:1.0'
    compile 'com.github.semoncat.seekarc:library:0.1'
    compile 'com.sothree.slidinguppanel:library:3.1.1'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'

    compile 'com.github.kabouzeid:Android-Universal-Image-Loader:8ffb5d4afa'

    compile('com.afollestad.material-dialogs:core:0.8.3.0@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.3.0@aar') {
        transitive = true
    }
    compile 'com.afollestad:material-cab:0.1.5'

    compile 'com.jakewharton:butterknife:7.0.1'

    //noinspection GradleDynamicVersion
    compile 'com.anjlab.android.iab.v3:library:1.0.+@aar'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
