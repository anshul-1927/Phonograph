buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

// For pushing APKs directly to Google Play. Won't work without the .p12 key.
play {
    serviceAccountEmail = '992499570703-v2bp8sl246qm0tblmfv4s45v6njhdfr2@developer.gserviceaccount.com'
    pk12File = file('../privatekeys/play-developer-key.p12')
    track = 'beta'
}

android {
    signingConfigs {
        debug {
            keyAlias 'phonograph-debug'
            keyPassword 'debugkey'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'
        }
        release {
            keyAlias 'phonograph'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'

            // "keystore.properties" file containing the release keys password is needed in order to sign the apk for release
            File propFile = project.rootProject.file('privatekeys/keystore.properties')
            if (propFile.exists()) {
                Properties properties = new Properties()
                properties.load(propFile.newDataInputStream())

                keyPassword properties.getProperty('keyPassword')
            } else {
                println 'keystore.properties not found'
                keyPassword null
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion '24.0.0 rc1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23

        renderscriptTargetApi 23
        vectorDrawables.useSupportLibrary = true

        applicationId "com.kabouzeid.gramophone"
        versionCode 105
        versionName "0.10.0 PREVIEW 2"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            ext.enableCrashlytics = false // Disable fabric build ID generation for debug builds

            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }

    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.6@aar') {
        transitive = true
    }

    compile('com.github.afollestad.material-dialogs:core:0.8.5.6@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.6@aar') {
        transitive = true
    }

    compile('com.afollestad:material-cab:0.1.9@aar') {
        transitive = true
    }

    compile('com.github.kabouzeid:app-theme-helper:1.0.1@aar') {
        transitive = true
    }

    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:support-v13:23.2.1'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:gridlayout-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.android.support:palette-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:support-annotations:23.2.1'
    compile 'com.android.support:percent:23.2.1'
    compile 'com.android.support:preference-v7:23.2.1'
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'com.github.kabouzeid:SeekArc:1.2-kmod'
    compile 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.1-kmod'
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    //noinspection GradleDynamicVersion
    compile 'com.anjlab.android.iab.v3:library:1.0.+'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.github.kabouzeid:RecyclerView-FastScroll:1.8-kmod'
    compile 'com.heinrichreimersoftware:material-intro:b8ec16d3d6'
}
