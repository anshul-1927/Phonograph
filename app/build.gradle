buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

// For pushing APKs directly to Google Play. Won't work without the .p12 key.
play {
    serviceAccountEmail = '992499570703-v2bp8sl246qm0tblmfv4s45v6njhdfr2@developer.gserviceaccount.com'
    pk12File = file('../privatekeys/play-developer-key.p12')
    track = 'beta'
}

android {
    signingConfigs {
        debug {
            keyAlias 'phonograph-debug'
            keyPassword 'debugkey'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'
        }
        release {
            keyAlias 'phonograph'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'

            // "keystore.properties" file containing the release keys password is needed in order to sign the apk for release
            File propFile = project.rootProject.file('privatekeys/keystore.properties')
            if (propFile.exists()) {
                Properties properties = new Properties()
                properties.load(propFile.newDataInputStream())

                keyPassword properties.getProperty('keyPassword')
            } else {
                println 'keystore.properties not found'
                keyPassword null
            }
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25

        renderscriptTargetApi 25
        vectorDrawables.useSupportLibrary = true

        applicationId "com.kabouzeid.gramophone"
        versionCode 124
        versionName "0.13.5"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            ext.enableCrashlytics = false // Disable fabric build ID generation for debug builds

            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true
    }

    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.0@aar') {
        transitive = true
    }

    compile('com.github.kabouzeid:app-theme-helper:1.3.3') {
        transitive = true
    }

    compile 'com.android.support:support-compat:25.1.0'
    compile 'com.android.support:support-core-utils:25.1.0'
    compile 'com.android.support:support-core-ui:25.1.0'
    compile 'com.android.support:support-media-compat:25.1.0'
    compile 'com.android.support:support-fragment:25.1.0'
    compile 'com.android.support:support-v13:25.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:gridlayout-v7:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:palette-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-annotations:25.1.0'
    compile 'com.android.support:percent:25.1.0'
    compile 'com.android.support:preference-v7:25.1.0'
    compile 'com.android.support:preference-v14:25.1.0'

    compile 'com.afollestad.material-dialogs:core:0.9.1.0@aar'
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0@aar'
    compile 'com.afollestad:material-cab:0.1.12@aar'

    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'com.github.kabouzeid:SeekArc:1.2-kmod'
    compile 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    //noinspection GradleDynamicVersion
    compile 'com.anjlab.android.iab.v3:library:1.0.+'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.github.kabouzeid:RecyclerView-FastScroll:1.9-kmod'
    compile 'com.heinrichreimersoftware:material-intro:1.6'
    compile 'me.zhanghai.android.materialprogressbar:library:1.3.0'
    compile 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}
